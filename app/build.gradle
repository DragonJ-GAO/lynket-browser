buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.2'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.3'

    defaultConfig {
        applicationId "arun.com.chromer"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 35
        versionName "1.7.2"

        buildConfigField "String", "BASE_64", getBase64()
        manifestPlaceholders = [crashlyticsApiKey: getFabricKey()]

        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Chromer"

            lintOptions {
                disable 'MissingTranslation'
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '- dev'
            resValue "string", "app_name", "Chromer-dev"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    // Debugging
    debugCompile 'com.facebook.stetho:stetho:1.4.2'

    // Dependency Injection
    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    // Support
    compile 'com.android.support:design:26.0.0'
    compile 'com.android.support:customtabs:26.0.0'
    compile 'com.android.support:preference-v14:26.0.0'
    compile 'com.android.support:cardview-v7:26.0.0'
    compile 'com.android.support:palette-v7:26.0.0'

    // Suggestions
    compile 'in.arunkumarsampath:suggestions:1.0.0'

    // Intro
    compile 'com.github.paolorotolo:appintro:4.1.0'

    // Icons
    compile 'com.mikepenz:community-material-typeface:1.9.32.1@aar'

    // Drawer
    compile('com.mikepenz:materialdrawer:5.3.6@aar') {
        transitive = true
    }

    // Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.4.2'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5'

    // Butterknife, Timber
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'

    // MVP
    compile 'com.hannesdorfmann.mosby3:mvp:3.0.4'

    // Image loading
    compile 'com.github.bumptech.glide:glide:3.7.0'

    // Bottom sheet
    compile 'com.flipboard:bottomsheet-core:1.5.1'
    compile 'com.flipboard:bottomsheet-commons:1.5.1'

    // Animations
    compile 'com.facebook.rebound:rebound:0.3.8'

    // Badge
    compile 'com.github.nekocode:Badge:1.2'

    // Reactive
    compile 'io.reactivex:rxjava:1.3.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:1.0.1'

    // Extraction
    compile 'com.github.chimbori:crux:-SNAPSHOT'

    // Storage
    compile 'io.paperdb:paperdb:2.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true
    }
    compile project(path: ':article-view')
}

static def getBase64() {
    try {
        Properties properties = new Properties()
        properties.load(new FileInputStream("values.properties"))
        return "\"" + properties.getProperty("BASE_64") + "\""
    } catch (Exception ignored) {
        return "\"\""
    }
}

static def getFabricKey() {
    try {
        Properties properties = new Properties()
        properties.load(new FileInputStream("values.properties"))
        return properties.getProperty("FABRIC_KEY")
    } catch (Exception ignored) {
        return "\"\""
    }
}